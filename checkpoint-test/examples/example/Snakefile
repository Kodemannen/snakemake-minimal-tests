


# a target rule to define the desired final output
rule all:
    input:
        "aggregated/a.txt",
        "aggregated/b.txt"


# the checkpoint that shall trigger re-evaluation of the DAG
checkpoint somestep:
    input:
        "samples/{case_id}.txt"
    output:
        "somestep/{case_id}.txt"
    shell:
        # simulate some output value
        "echo {wildcards.case_id} > somestep/{wildcards.case_id}.txt"


# intermediate rule
rule intermediate:
    input:
        "somestep/{case_id}.txt"
    output:
        "post/{case_id}.txt"
    shell:
        "touch {output}"


# alternative intermediate rule
rule alt_intermediate:
    input:
        "somestep/{case_id}.txt"
    output:
        "alt/{case_id}.txt"
    shell:
        "touch {output}"


# input function for the rule aggregate
def aggregate_input(wildcards):
    # decision based on content of output file
    # Important: use the method open() of the returned file!
    # This way, Snakemake is able to automatically download the file if it is generated in
    # a cloud environment without a shared filesystem.
    print(checkpoints.somestep.get(case_id=wildcards.case_id))

    with checkpoints.somestep.get(case_id=wildcards.case_id).output[0].open() as f:
        if f.read().strip() == "a":
            return "post/{case_id}.txt"
        else:
            return "alt/{case_id}.txt"


rule aggregate:
    input:
        aggregate_input
    output:
        "aggregated/{case_id}.txt"
    shell:
        "touch {output}"
